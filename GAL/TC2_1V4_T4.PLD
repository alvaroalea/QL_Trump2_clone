Name            TC2_1V4_T4;
Partno          ;
Revision        02;
Date            14/08/2024;
Designer        Alvaro Alea;
Company         None;
Location        None;
Assembly        None;
Device          g16v8;

/* Inputs */
pin 1 = !DSL;      /*  */
pin 2 = A19;      /*  */
pin 3 = A18;      /*  */
pin 4 = A17;      /*  */
pin 5 = A16;      /*  */
pin 6 = A15;      /*  */
pin 7 = RST;      /*  */
pin 8 = A14;      /*  */
pin 9 = A13;      /*  */
pin 11 = MUX;      /*  */

/* Outputs */
pin 12 = DSMC;     /*  */
pin 13 = P13;      /*  */
pin 14 = CLKCT;    /*  */
pin 15 = MA8;      /*  */
pin 17 = !FDC_CS;   /*  */
pin 18 = !U17_CS;   /*  */
pin 19 = !ROM_CS;   /*  */

/* Pinnodes */
pinnode 16 = P16;      /*  */

/* equations */

/* MEMORY MAPPING */

ROM2 =  A19 &  A18 & !A17 & !A16 & !A15 ;      /* 16Kb on 0xC0000  */
RAM3 =  A19 &  A18 ;                           /*256Kb on 0xC0000  */
RAM2 =  A19 & !A18 ;                           /*256Kb on 0x80000  */
RAM1 = !A19 &  A18 ;                           /*256Kb on 0x40000  */
ROM1 = !A19 & !A18 & !A17 &  A16 & !A15 ;      /* 32Kb on 0x10000  */
IODIR =!A19 & !A18 & !A17 &  A16 &  A15 & A14 ;/* 16Kb on 0x1C000  */

/* CHIPS CSs */

U17_CS = DSL & IODIR &  A13;        /* 8Kb on 0x1E000-0x1FFFF  */
FDC_CS = DSL & IODIR & !A13;        /* 8Kb on 0x1C000-0x1DFFF  */
ROM_CS = ( (DSL & ROM1 )
         # (DSL & ROM2 & !P13)
         ) ;

/* DISABLE INTERNAL QL CIRCUITS DECODING */

DSMC  =  ( (DSL & IODIR)
         # (DSL & ROM1 )
         # (DSL & ROM2 & !P13)
         # (DSL & RAM3 &  P13)
         # (DSL & RAM2)
         # (DSL & RAM1)
         );

/* RAM REFRESH RELATED */

CLKCT = DSL ;
MA8 = !( (A17 & MUX ) # (A16 & !MUX) );

/* FLIP-FLOP FOR ROM BANKING */

P16 = !( ( DSL & !A19 & !A18 & !A17 & A16 & !A15 & !A14 )
       # ( RST & !P16 )
       );

P13 = !( (  DSL & !A19 & !A18 & !A17 & !A16 & A15 & A14 & P16)
       # (  A19 & RST & !P13 )
       # (  A18 & RST & !P13 )
       # (  A17 & RST & !P13 )
       # ( !A16 & RST & !P13 )
       # (  A15 & RST & !P13 )
       # ( !DSL & RST & !P13 )
       );
